! Copyright (C) 2020 Bolding & Bruggeman and Hans Burchard

!#define _AUTOMATIC_

   USE, INTRINSIC :: ISO_FORTRAN_ENV
   USE advection_base

   IMPLICIT NONE

   private

   type, extends(type_advection_base), public :: _TYPE_NAME_
   contains
       procedure, nopass :: u2d
       procedure, nopass :: v2d
       procedure, nopass :: w3d
   end type

contains

!---------------------------------------------------------------------------

SUBROUTINE u2d(imin,imax,jmin,jmax,halo,umask,dxu,dyu,hu,u,tmask,iA,dt,h,f)
   ! Subroutine arguments
   integer, intent(in) :: imin,imax,jmin,jmax
   integer, intent(in) :: halo(2)
#define _A_  imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)
   integer, intent(in) :: umask(_A_)
   real(real64), intent(in) :: dxu(_A_)
   real(real64), intent(in) :: dyu(_A_)
   real(real64), intent(in) :: hu(_A_)
   real(real64), intent(in) :: u(_A_)
   integer, intent(in) :: tmask(_A_)
   real(real64), intent(in) :: iA(_A_)
   real(real64), intent(in) :: dt
   real(real64), intent(inout) :: h(_A_)
   real(real64), intent(inout) :: f(_A_)
#undef _A_

!  Local constants

!  Local variables
#ifdef _AUTOMATIC_
   real(real64) :: flux(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2))
   real(real64) ::   QU(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2))
#else
   real(real64), allocatable :: flux(:,:), QU(:,:)
#endif
   real(real64) :: cfl, fu, fuu, fd, hfo, advn
   real(real64) :: deltaf, deltafu, ratio
   real(real64) :: limiter
   integer :: i, j
!---------------------------------------------------------------------------
#ifndef _AUTOMATIC_
   allocate(flux(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)))
   allocate(  QU(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)))
#endif

   ! the provided velocity MUST be ZERO at land!
   flux(imin-halo(1),:) = 0._real64
     QU(imin-halo(1),:) = 0._real64

!  TODO: (vertical) interation!!!
   do j=jmin,jmax
      do i=imin-1,imax
         QU(i,j) = u(i,j) * hu(i,j) * dyu(i,j)
         if (umask(i,j) == 1 .or. umask(i,j) == 2) then
            cfl = abs(u(i,j)*dt/dxu(i,j))
            if (u(i,j) .gt. 0) then
               fu  = f(i,j)
               fuu = fu
!               if (i > imin) then
                  if (umask(i-1,j) == 1 .or. umask(i-1,j) == 2) fuu = f(i-1,j)
!               end if
               fd = f(i+1,j)
            else
               fu  = f(i+1,j)
               fuu = fu
!               if (i < imax-1) then
                  if (umask(i+1,j) == 1 .or. umask(i+1,j) == 2) fuu = f(i+2,j)
!               end if
               fd = f(i,j)
            end if

            deltaf  = fd - fu
            deltafu = fu - fuu
            if (deltaf*deltafu .gt. 0) then
               ratio = deltafu / deltaf   ! slope ratio
#include "limiters.h"
               fu = fu + 0.5*limiter*(1-cfl)*deltaf
            end if
            flux(i,j) = QU(i,j)*fu
         else
            flux(i,j) = 0._real64
         end if
      end do
   end do

   do j=jmin,jmax
      do i=imin,imax
         if (tmask(i,j) == 1) then
            hfo = h(i,j)*f(i,j)
            h(i,j) = h(i,j) - dt*( QU(i,j)-QU(i-1,j) )*iA(i,j)
            advn = ( flux(i,j)-flux(i-1,j) )*iA(i,j)
            f(i,j) = ( hfo - dt*advn ) / h(i,j)
         end if
      end do
   end do

!#ifndef _AUTOMATIC_
!   deallocate(flux, QU)
!#endif
END SUBROUTINE u2d

SUBROUTINE v2d(imin,imax,jmin,jmax,halo,vmask,dxv,dyv,hv,v,tmask,iA,dt,h,f)
   ! Subroutine arguments
   integer, intent(in) :: imin,imax,jmin,jmax
   integer, intent(in) :: halo(2)
#define _A_  imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)
   integer, intent(in) :: vmask(_A_)
   real(real64), intent(in) :: dxv(_A_)
   real(real64), intent(in) :: dyv(_A_)
   real(real64), intent(in) :: hv(_A_)
   real(real64), intent(in) :: v(_A_)
   integer, intent(in) :: tmask(_A_)
   real(real64), intent(in) :: iA(_A_)
   real(real64), intent(in) :: dt
   real(real64), intent(inout) :: h(_A_)
   real(real64), intent(inout) :: f(_A_)
#undef _A_

!  Local constants

!  Local variables
#ifdef _AUTOMATIC_
   real(real64) :: flux(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2))
   real(real64) ::   QV(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2))
#else
   real(real64), allocatable :: flux(:,:), QV(:,:)
#endif
   real(real64) :: cfl, fu, fuu, fd, hfo, advn
   real(real64) :: deltaf, deltafu, ratio
   real(real64) :: limiter
   integer :: i, j
!---------------------------------------------------------------------------
#ifndef _AUTOMATIC_
   allocate(flux(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)))
   allocate(  QV(imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)))
#endif

   ! the provided velocity MUST be ZERO at land!
   flux(:,jmin-halo(2)) = 0._real64
     QV(:,jmin-halo(2)) = 0._real64

!  TODO: (vertical) interation!!!
   do j=jmin-1,jmax
      do i=imin,imax
         QV(i,j) = v(i,j) * hv(i,j) * dxv(i,j)
         if (vmask(i,j) == 1 .or. vmask(i,j) == 2) then
            cfl = abs(v(i,j)*dt/dyv(i,j))
            if (v(i,j) .gt. 0) then
               fu  = f(i,j)
               fuu = fu
!               if (j > jmin) then
                  if (vmask(i,j-1) == 1 .or. vmask(i,j-1) == 2) fuu = f(i,j-1)
!               end if
               fd = f(i,j+1)
            else
               fu  = f(i,j+1)
               fuu = fu
!               if (j < jmax-1) then
                  if (vmask(i,j+1) == 1 .or. vmask(i,j+1) == 2) fuu = f(i,j+2)
!               end if
               fd = f(i,j)
            end if

            deltaf  = fd - fu
            deltafu = fu - fuu
            if (deltaf*deltafu .gt. 0) then
               ratio = deltafu / deltaf   ! slope ratio
#include "limiters.h"
               fu = fu + 0.5*limiter*(1-cfl)*deltaf
            end if
            flux(i,j) = QV(i,j)*fu
         else
            flux(i,j) = 0._real64
         end if
      end do
   end do

   do j=jmin,jmax
      do i=imin,imax
         if (tmask(i,j) == 1) then
            hfo = h(i,j)*f(i,j)
            h(i,j) = h(i,j) - dt*( QV(i,j)-QV(i,j-1) )*iA(i,j)
            advn = ( flux(i,j)-flux(i,j-1) )*iA(i,j)
            f(i,j) = ( hfo - dt*advn ) / h(i,j)
         end if
      end do
   end do

!#ifndef _AUTOMATIC_
!   deallocate(flux, QV)
!#endif
END SUBROUTINE v2d

SUBROUTINE w3d(imin,imax,jmin,jmax,kmax,halo,w,tmask,dt,h,f)
   ! Subroutine arguments
   integer, intent(in) :: imin, imax, jmin, jmax, kmax
   integer, intent(in) :: halo(2)
#define _2D_  imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2)
#define _3D_  imin-halo(1):imax+halo(1),jmin-halo(2):jmax+halo(2),0:kmax
   real(real64), intent(in) :: w(_3D_)
   integer, intent(in) :: tmask(_2D_)
   real(real64), intent(in) :: dt
   real(real64), target, intent(inout) :: h(_3D_)
   real(real64), target, intent(inout) :: f(_3D_)
#undef _2D_
#undef _3D_

!  Local constants
   integer, parameter :: itersmax=200

!  Local variables
#ifdef _AUTOMATIC_
   real(real64), dimension(0:kmax) :: flux, cfl0, cfls
   real(real64), dimension(0:kmax), target :: haux, faux
#else
   real(real64), dimension(:), allocatable, save :: flux, cfl0, cfls
   real(real64), dimension(:), allocatable, target, save :: haux, faux
#endif
   real(real64), dimension(:), pointer :: p_haux, p_faux, p1d
   real(real64) :: itersm1, dtk
   real(real64) :: cfl, fu, fuu, fd, hfo, advn
   real(real64) :: deltaf, deltafu, ratio
   real(real64) :: limiter
   integer :: i, j, k, it, iters, iters_new
   logical, save :: first=.true.
!---------------------------------------------------------------------------
   if (first) then
#ifndef _AUTOMATIC_
      allocate(flux(0:kmax), cfl0(0:kmax), cfls(0:kmax), haux(0:kmax), faux(0:kmax))
#endif
      first = .false.
   end if

   flux(0   ) = 0._real64
   flux(kmax) = 0._real64


   do j=jmin,jmax
      do i=imin,imax
         if (tmask(i,j) .eq. 1) then

            do k=1,kmax-1
               cfl0(k) = ABS( w(i,j,k) ) * dt / MIN( h(i,j,k) , h(i,j,k+1) )
            end do

            iters = 1
            itersm1 = 1._real64
            dtk = dt

            p_haux => haux
            p_faux => faux

            it = 1

            do while (it .le. iters)

               if (it .eq. 1) then
                  haux = h(i,j,:)
                  faux = f(i,j,:)
                  cfls = cfl0
               else
                  do k=1,kmax-1
                     cfls(k) = ABS( w(i,j,k) ) * dt / MIN( haux(k) , haux(k+1) )
                  end do
               end if

               if (iters .lt. itersmax) then
!                 estimate number of iterations by maximum cfl number in water column
                  cfl = MAXVAL( cfls(1-0:kmax-1) )
                  iters_new = MAX( 1 , CEILING( cfl ) )
                  if (iters_new .gt. iters) then
                     if (iters_new .gt. itersmax) then
                        !STDERR 'adv_split_w: too many iterations needed: ',iters_new
                        !STDERR '             at global i j = ',ioff+i,joff+j
                        !STDERR '             cfl=',real(cfl/itersmax)
                        iters = itersmax
                     else
                        iters = iters_new
                     end if
                     itersm1 = 1._real64 / iters
                     dtk = dt * itersm1
                     if (it .gt. 1) then
#ifdef DEBUG
                        !STDERR 'adv_split_w: restart iterations during it=',it
                        !STDERR 'i=',i,' j=',j,':',iters
#endif
                        it = 1
                        cycle
                     end if
                  end if
               end if

!              Calculating w-interface fluxes !
               do k=1,kmax-1
                  cfl = cfls(k)*itersm1
                  if (w(i,j,k) .gt. 0) then
                     fu = faux(k)
                     if (k .gt. 1) then
                        fuu = faux(k-1)
                     else
                        fuu = fu
                     end if
                     fd  = faux(k+1)
                  else
                     fu = faux(k+1)
                     if (k .lt. kmax-1) then
                        fuu = faux(k+2)
                     else
                        fuu = fu
                     end if
                     fd  = faux(k  )
                  end if
                  deltaf  = fd - fu
                  deltafu = fu - fuu
                  if (deltaf*deltafu .gt. 0) then
                     ratio = deltafu / deltaf   ! slope ratio
#include "limiters.h"
                     fu = fu + 0.5*limiter*(1-cfl)*deltaf
                  end if
                  flux(k) = w(i,j,k)*fu
               end do

               if (it .eq. iters) then
                  p1d => h(i,j,:) ; p_haux(0:) => p1d
                  p1d => f(i,j,:) ; p_faux(0:) => p1d
               end if

               do k=1,kmax
                  hfo = haux(k)*faux(k)
                  p_haux(k) = haux(k) - dtk*(w(i,j,k  )-w(i,j,k-1))
                  advn = flux(k  ) - flux(k-1)
                  p_faux(k) = ( hfo - dtk*advn ) / p_haux(k)
               end do

               it = it + 1

            end do

         end if

      end do
   end do
END SUBROUTINE w3d

